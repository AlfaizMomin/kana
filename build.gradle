buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

group = 'mariten'
version = '1.2.1'
description = 'Utilities for handling Japanese kana text easily in Java'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'junit:junit:4.11'
}

test {
    systemProperty 'test_with_php', "$System.env.TEST_WITH_PHP"
}

jacoco {
    toolVersion = '0.7.5.201505241946'
    reportsDir = file("${buildDir}/coverage")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

// Custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = 'mariten'
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'kanatools-java'
        desc = 'Utilities for handling Japanese kana text easily in Java'
        licenses = ['Apache-2.0']

        websiteUrl = 'http://mariten.github.io/kanatools-java'
        issueTrackerUrl = 'https://github.com/mariten/kanatools-java/issues'
        vcsUrl = 'https://github.com/mariten/kanatools-java.git'

        githubRepo = 'mariten/kanatools-java'
        githubReleaseNotesFile = 'HISTORY.md'

        publicDownloadNumbers = true
        labels = ['japanese', 'conversion', 'kana', 'hiragana', 'katakana', 'romaji', 'zenkaku', 'hankaku']

        version {
            name = '1.2.1'
            vcsTag = '1.2.1'
            released  = new Date()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
